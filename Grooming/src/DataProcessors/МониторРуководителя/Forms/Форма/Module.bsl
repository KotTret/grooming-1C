
#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ПериодОбработки = НачалоМесяца(ТекущаяДатаСеанса());
	МесяцСтрокой = Формат(ПериодОбработки, "ДФ='ММММ гггг'");
	ЗаполнитьДанныеНаСервере();
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаКлиенте
Процедура МесяцСтрокойНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Подсказка = "Введите период получения данных";
	ЧастьДаты = ЧастиДаты.Дата;
	Оповещение = Новый ОписаниеОповещения("ПослеВводаПериода", ЭтотОбъект);
	ПоказатьВводДаты(Оповещение, ПериодОбработки, Подсказка, ЧастьДаты);
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура Обновить(Команда)
	ЗаполнитьДанныеНаСервере();
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаСервере
Процедура ЗаполнитьДанныеНаСервере()
	ЗаполнитьЧисловыеПоказатели();
	ЗаполнитьГистограммы();
	ЗаполнитьКруговыеДиаграммы();
	ЗаполнитьКоличетсвоДоляПитомцев();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьКоличетсвоДоляПитомцев()

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КонтрагентыПитомцы.ВидПитомца КАК ВидПитомца,
	|	СУММА(1) КАК Количество
	|ПОМЕСТИТЬ ВТ_Питомцы
	|ИЗ
	|	Справочник.Контрагенты.Питомцы КАК КонтрагентыПитомцы
	|ГДЕ
	|	КонтрагентыПитомцы.Ссылка.ТипКонтрагента = ЗНАЧЕНИЕ(Перечисление.ТипыКонтрагентов.Клиент)
	|
	|СГРУППИРОВАТЬ ПО
	|	КонтрагентыПитомцы.ВидПитомца
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(СУММА(ВТ_Питомцы.Количество), 0) КАК Количество
	|ИЗ
	|	ВТ_Питомцы КАК ВТ_Питомцы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Питомцы.ВидПитомца КАК ВидПитомца,
	|	ВТ_Питомцы.Количество КАК Количество
	|ИЗ
	|	ВТ_Питомцы КАК ВТ_Питомцы";

	РезультатПакет = Запрос.ВыполнитьПакет();

	ВыгрузкаКоличество = РезультатПакет[1].Выбрать();
	ВыгрузкаКоличество.Следующий();
	КоличетсвоПитомцев = ВыгрузкаКоличество.Количество;

	Если КоличетсвоПитомцев <> 0 Тогда
		МассивСтрок = Новый Массив;
		КоличествоДоляПитомцевСтрока = "ВидПитомцев - Количество (Доля)";
		ВыгрузкаПитомцев = РезультатПакет[2].Выбрать();
		Пока ВыгрузкаПитомцев.Следующий() Цикл
			МассивСтрок.Добавить(СтрШаблон("%1 - %2 (%3)", ВыгрузкаПитомцев.ВидПитомца, ВыгрузкаПитомцев.Количество,
				Окр(ВыгрузкаПитомцев.Количество / КоличетсвоПитомцев * 100, 2)));

		КонецЦикла;
	Иначе
		КоличествоДоляПитомцевСтрока = "Питомцев пока не подвезли";
	КонецЕсли;
	КоличествоДоляПитомцевСтрока = СтрСоединить(МассивСтрок, Символы.ПС);
КонецПроцедуры
&НаСервере
Процедура ЗаполнитьЧисловыеПоказатели()
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПродажиОбороты.Регистратор КАК Регистратор,
	|	ПродажиОбороты.СуммаОборот КАК СуммаОборот
	|ПОМЕСТИТЬ ВТ_Продажи
	|ИЗ
	|	РегистрНакопления.Продажи.Обороты(&ДатаНачала, &ДатаОкончания, Регистратор, ) КАК ПродажиОбороты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ВТ_Продажи.СуммаОборот) КАК СуммаОборот
	|ИЗ
	|	ВТ_Продажи КАК ВТ_Продажи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ВТ_Продажи.СуммаОборот) КАК СуммаОборот,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВТ_Продажи.Регистратор) КАК Регистратор
	|ПОМЕСТИТЬ ВТ_КоличествоРегистраторов
	|ИЗ
	|	ВТ_Продажи КАК ВТ_Продажи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ВТ_КоличествоРегистраторов.Регистратор = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(ВТ_КоличествоРегистраторов.СуммаОборот / ВТ_КоличествоРегистраторов.Регистратор КАК ЧИСЛО(10, 2))
	|	КОНЕЦ КАК СреднийЧек
	|ИЗ
	|	ВТ_КоличествоРегистраторов КАК ВТ_КоличествоРегистраторов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаказыКлиентов.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ВТ_ЗаписиКлиентов
	|ИЗ
	|	РегистрНакопления.ЗаказыКлиентов КАК ЗаказыКлиентов
	|ГДЕ
	|	ЗаказыКлиентов.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И ЗаказыКлиентов.Регистратор ССЫЛКА Документ.ЗаписьКлиента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВТ_ЗаписиКлиентов.Регистратор) КАК КоличествоЗаписейКлиентов
	|ИЗ
	|	ВТ_ЗаписиКлиентов КАК ВТ_ЗаписиКлиентов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВТ_ЗаписиКлиентов.Регистратор) КАК Завершенных
	|ИЗ
	|	ВТ_ЗаписиКлиентов КАК ВТ_ЗаписиКлиентов
	|ГДЕ
	|	ВТ_ЗаписиКлиентов.Регистратор.УслугаОказана";

	Запрос.УстановитьПараметр("ДатаНачала", ПериодОбработки);
	Запрос.УстановитьПараметр("ДатаОкончания", КонецМесяца(ПериодОбработки));

	ВсегоЗаписей = 0;

	РезультатПакет = Запрос.ВыполнитьПакет();

	ВыборкаПродажи = РезультатПакет[1].Выбрать();
	Если ВыборкаПродажи.Следующий() Тогда
		ВыручкаЧисло = ВыборкаПродажи.СуммаОборот;
	КонецЕсли;

	ВыборкаСреднийЧек = РезультатПакет[3].Выбрать();
	Если ВыборкаСреднийЧек.Следующий() Тогда
		СреднийЧек = ВыборкаСреднийЧек.СреднийЧек;
	КонецЕсли;

	ВыборкаЗаписиКлиентов = РезультатПакет[5].Выбрать();
	Если ВыборкаЗаписиКлиентов.Следующий() Тогда
		ВсегоЗаписей = ВыборкаЗаписиКлиентов.КоличествоЗаписейКлиентов;
	КонецЕсли;

	ВыборкаЗаписиКлиентовЗавершенные = РезультатПакет[6].Выбрать();
	Если ВыборкаЗаписиКлиентовЗавершенные.Следующий() Тогда
		Завершенных = ВыборкаЗаписиКлиентовЗавершенные.Завершенных;
		Если ВсегоЗаписей > 0 Тогда
			ПроцентЗавершенных = ОКР((100 * Завершенных / ВсегоЗаписей), 2);
			ЗавершенныхПроцентСтрока = СтрШаблон("Это %1 процентов от всех записей", ПроцентЗавершенных);
		Иначе
			ЗавершенныхПроцентСтрока = "В этом периоде нет записей клиентов!";
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьГистограммы()
	ДиаграммаВыручка.Обновление = Ложь;
	ДиаграммаВыручка.Очистить();

	Серия = ДиаграммаВыручка.Серии.Добавить("Оборот");
	Серия.Цвет = WebЦвета.Аквамарин;

	Выборка = ДанныеДляЗаполненияДиаграмм("Гистограмма", ПериодОбработки);
	Пока Выборка.Следующий() Цикл
		Точка = ДиаграммаВыручка.Точки.Добавить(Выборка.Период);
		Точка.Текст = Формат(Выборка.Период, "ДФ=dd.MM.yy");
		Точка.Расшифровка = Выборка.Период;
		Подсказка = "Выручка " + Выборка.СуммаОборот + " на " + Формат(Выборка.Период, "ДФ=dd.MM.yyyy");
		ДиаграммаВыручка.УстановитьЗначение(Точка, Серия, Выборка.СуммаОборот, Точка.Расшифровка, Подсказка);
	КонецЦикла;
	ДиаграммаВыручка.Обновление = Истина;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьКруговыеДиаграммы()

	ЗаполнитьДиаграммуПоИсточникамИнформации();
	ЗаполнитьДиаграммуПоСотрудникам();
	ЗаполнитьДиаграммуПоУслугам();

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДиаграммуПоУслугам()

	ДиаграммаПоУслугам.Обновление = Ложь;
	ДиаграммаПоУслугам.Очистить();

	Точка = ДиаграммаПоУслугам.Точки.Добавить("Сумма");
	Точка.Текст = "Сумма";
	Точка.ПриоритетЦвета = Ложь;
	Выборка = ДанныеДляЗаполненияДиаграмм("ДиаграммаПоУслугам", ПериодОбработки);
	Пока Выборка.Следующий() Цикл
		Серия = ДиаграммаПоУслугам.Серии.Добавить(Выборка.Номенклатура);
		ДиаграммаПоУслугам.УстановитьЗначение(Точка, Серия, Выборка.Сумма, Строка(Выборка.Сумма));
	КонецЦикла;

	ДиаграммаПоУслугам.Обновление = Истина;
КонецПроцедуры
&НаСервере
Процедура ЗаполнитьДиаграммуПоСотрудникам()

	ДиаграммаВыручкаПоСотрудникам.Обновление = Ложь;
	ДиаграммаВыручкаПоСотрудникам.Очистить();

	Точка = ДиаграммаВыручкаПоСотрудникам.Точки.Добавить("Сумма");
	Точка.Текст = "Сумма";
	Точка.ПриоритетЦвета = Ложь;
	Выборка = ДанныеДляЗаполненияДиаграмм("ДиаграммаПоСотрудникам", ПериодОбработки);

	Пока Выборка.Следующий() Цикл
		Серия = ДиаграммаВыручкаПоСотрудникам.Серии.Добавить(Выборка.Сотрудник);
		ДиаграммаВыручкаПоСотрудникам.УстановитьЗначение(Точка, Серия, Выборка.Сумма, Строка(Выборка.Сумма));
	КонецЦикла;

	ДиаграммаВыручкаПоСотрудникам.Обновление = Истина;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДиаграммуПоИсточникамИнформации()

	ДиаграммаПоРекламнымИсточникам.Обновление = Ложь;
	ДиаграммаПоРекламнымИсточникам.Очистить();

	Точка = ДиаграммаПоРекламнымИсточникам.Точки.Добавить("Сумма");
	Точка.Текст = "Сумма";
	Точка.ПриоритетЦвета = Ложь;
	Выборка = ДанныеДляЗаполненияДиаграмм("ДиаграммаПоИсточникамИнформации", ПериодОбработки);
	Пока Выборка.Следующий() Цикл
		Серия = ДиаграммаПоРекламнымИсточникам.Серии.Добавить(Выборка.ИсточникИнформации);
		ДиаграммаПоРекламнымИсточникам.УстановитьЗначение(Точка, Серия, Выборка.Сумма, Строка(Выборка.Сумма));
	КонецЦикла;

	ДиаграммаПоРекламнымИсточникам.Обновление = Истина;
КонецПроцедуры
&НаСервереБезКонтекста
Функция ДанныеДляЗаполненияДиаграмм(Диаграмма, ПериодОбработки)
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(ПродажиОбороты.Период, ДЕНЬ) КАК Период,
	|	ПродажиОбороты.СуммаОборот КАК СуммаОборот
	|ИЗ
	|	РегистрНакопления.Продажи.Обороты(&ДатаНачала, &ДатаОкончания, Регистратор, ) КАК ПродажиОбороты
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период
	|ИТОГИ
	|	СУММА(СуммаОборот)
	|ПО
	|	Период ПЕРИОДАМИ(ДЕНЬ, &ДатаНачала, &ДатаОкончания)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПродажиОбороты.Номенклатура КАК Номенклатура,
	|	СУММА(ПродажиОбороты.СуммаОборот) КАК СуммаОборот
	|ПОМЕСТИТЬ ВТ_КонкретныеУслуги
	|ИЗ
	|	РегистрНакопления.Продажи.Обороты(&ДатаНачала, &ДатаОкончания, Регистратор, Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга)) КАК ПродажиОбороты
	|
	|СГРУППИРОВАТЬ ПО
	|	ПродажиОбороты.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ПродажиОбороты.СуммаОборот) КАК СуммаОборот
	|ПОМЕСТИТЬ ВТ_ВсеПродажи
	|ИЗ
	|	РегистрНакопления.Продажи.Обороты(&ДатаНачала, &ДатаОкончания, Регистратор, Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга)) КАК ПродажиОбороты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_КонкретныеУслуги.Номенклатура КАК Номенклатура,
	|	ВТ_КонкретныеУслуги.СуммаОборот КАК СуммаПродажНоменклатуры,
	|	ВТ_ВсеПродажи.СуммаОборот КАК ОбщаяСуммаПоУслугам
	|ПОМЕСТИТЬ ВТ_Предытог
	|ИЗ
	|	ВТ_КонкретныеУслуги КАК ВТ_КонкретныеУслуги
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ВсеПродажи КАК ВТ_ВсеПродажи
	|		ПО (ИСТИНА)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА 100 * ВТ_Предытог.СуммаПродажНоменклатуры / ВТ_Предытог.ОбщаяСуммаПоУслугам > 10
	|			ТОГДА ВТ_Предытог.Номенклатура.Представление
	|		ИНАЧЕ ""Прочее""
	|	КОНЕЦ КАК Номенклатура,
	|	СУММА(ВТ_Предытог.СуммаПродажНоменклатуры) КАК Сумма
	|ИЗ
	|	ВТ_Предытог КАК ВТ_Предытог
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР
	|		КОГДА 100 * ВТ_Предытог.СуммаПродажНоменклатуры / ВТ_Предытог.ОбщаяСуммаПоУслугам > 10
	|			ТОГДА ВТ_Предытог.Номенклатура.Представление
	|		ИНАЧЕ ""Прочее""
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ПродажиОбороты.СуммаОборот) КАК Сумма,
	|	ПродажиОбороты.Сотрудник.Представление КАК Сотрудник
	|ИЗ
	|	РегистрНакопления.Продажи.Обороты(&ДатаНачала, &ДатаОкончания, Регистратор, ) КАК ПродажиОбороты
	|
	|СГРУППИРОВАТЬ ПО
	|	ПродажиОбороты.Сотрудник.Представление
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ПродажиОбороты.Клиент.ИсточникИнформации.Представление, ""Не указан"") КАК ИсточникИнформации,
	|	СУММА(ПродажиОбороты.СуммаОборот) КАК Сумма
	|ИЗ
	|	РегистрНакопления.Продажи.Обороты(&ДатаНачала, &ДатаОкончания, Регистратор, ) КАК ПродажиОбороты
	|
	|СГРУППИРОВАТЬ ПО
	|	ЕСТЬNULL(ПродажиОбороты.Клиент.ИсточникИнформации.Представление, ""Не указан"")";

	Запрос.УстановитьПараметр("ДатаНачала", ПериодОбработки);
	Запрос.УстановитьПараметр("ДатаОкончания", КонецМесяца(ПериодОбработки));

	РезультатПакета = Запрос.ВыполнитьПакет();

	Если Диаграмма = "Гистограмма" Тогда
		Возврат РезультатПакета[0].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Период", "Все")
	ИначеЕсли
	Диаграмма = "ДиаграммаПоУслугам" Тогда
		Возврат РезультатПакета[4].Выбрать();
	ИначеЕсли Диаграмма = "ДиаграммаПоСотрудникам" Тогда
		Возврат РезультатПакета[5].Выбрать();
	ИначеЕсли Диаграмма = "ДиаграммаПоИсточникамИнформации" Тогда
		Возврат РезультатПакета[6].Выбрать();
	КонецЕсли;

КонецФункции
&НаКлиенте
Процедура ПослеВводаПериода(Результат, ДополнительныеПараметры) Экспорт
	Если Результат <> Неопределено Тогда
		ПериодОбработки = НачалоМесяца(Результат);
		МесяцСтрокой = Формат(ПериодОбработки, "ДФ='ММММ гггг'");
	КонецЕсли;
	ЗаполнитьДанныеНаСервере();
КонецПроцедуры
#КонецОбласти