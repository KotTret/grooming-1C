
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("Основание")
		И ЗначениеЗаполнено(Параметры.Основание)
		И ТипЗнч(Параметры.Основание) = Тип("ДокументСсылка.РеализацияТоваровИУслуг") Тогда
		
		СтруктураОплаты = Документы.РеализацияТоваровИУслуг.ПроверитьОплатуДокумента(Параметры.Основание);
		ПризнакОплаты = СтруктураОплаты.ПризнакОплаты;
		
		Если ПризнакОплаты = Перечисления.ОплатаДокумента.ПолностьюОплачен Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Данный документ уже полностью оплачен. Ввод дополнительного документа оплаты не требуется!";
			Сообщение.Сообщить();
			Отказ = Истина;
			Возврат;
		Иначе
			Объект.СуммаДокумента = СтруктураОплаты.ОсталосьОплатить;
			ОсталосьОплатить = СтруктураОплаты.ОсталосьОплатить;
			Объект.ВидОперации = Перечисления.ВидыОперацийПоступленияДенег.ОплатаОтПокупателя;
		КонецЕсли;
	КонецЕсли;
	
	
	ЗаполнитьРеквизитыФормы();
КонецПроцедуры  

&НаСервере
Процедура ЗаполнитьРеквизитыФормы()
	Плательщик = Объект.Плательщик;
	ВидОперации = Объект.ВидОперации;
	ТипДенежныхСредств = Объект.ТипДенежныхСредств;
	
	Если Объект.Ссылка.Пустая() Тогда
		Объект.ТипДенежныхСредств = Перечисления.ТипДенежныхСредств.ПустаяСсылка();
		Если  НЕ Объект.ВидОперации = Перечисления.ВидыОперацийПоступленияДенег.ОплатаОтПокупателя Тогда  
			Объект.ВидОперации = Перечисления.ВидыОперацийПоступленияДенег.ПустаяСсылка(); 
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры  

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьВидимость();
КонецПроцедуры


&НаКлиенте
Процедура УстановитьВидимость()
	СтрокаТипПлательщика = "";
	
	Если Объект.ТипДенежныхСредств = ПредопределенноеЗначение("Перечисление.ТипДенежныхСредств.Наличные") Тогда
		Элементы.Касса.Видимость = Истина;
		Элементы.ЭквайринговыйТерминал.Видимость = Ложь;
	ИначеЕсли Объект.ТипДенежныхСредств = ПредопределенноеЗначение("Перечисление.ТипДенежныхСредств.Безналичные") Тогда
		Элементы.Касса.Видимость = Ложь;
		Элементы.ЭквайринговыйТерминал.Видимость = Истина;
	КонецЕсли;
	
	
	Если Объект.ВидОперации =  ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступленияДенег.ВозвратОтПодотчетника") Тогда
		Элементы.ДоговорКонтрагента.Видимость = Ложь;          
		Элементы.Плательщик.Видимость = Истина;
		СтрокаТипПлательщика = "СправочникСсылка.Сотрудники";
	ИначеЕсли Объект.ВидОперации =  ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступленияДенег.ПолучениеНаличныхВБанке") Тогда
		Элементы.ДоговорКонтрагента.Видимость = Ложь;    
		Элементы.Плательщик.Видимость = Истина;
		Элементы.Касса.Видимость = Истина;
		Элементы.ЭквайринговыйТерминал.Видимость = Ложь;                                                                                                             
		СтрокаТипПлательщика = "СправочникСсылка.Банки";  
	ИначеЕсли Объект.ВидОперации =  ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступленияДенег.ОплатаОтПокупателя") Тогда
		Элементы.ДоговорКонтрагента.Видимость = Ложь;    
		Элементы.Плательщик.Видимость = Истина;                                                                                                             
		СтрокаТипПлательщика = "СправочникСсылка.Контрагенты";
		
	Иначе
		Элементы.ДоговорКонтрагента.Видимость = Истина;         
		Элементы.Плательщик.Видимость = Истина;
		СтрокаТипПлательщика = "СправочникСсылка.Контрагенты";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтрокаТипПлательщика) Тогда         
		Массив = Новый Массив();
		Массив.Добавить(Тип(СтрокаТипПлательщика));    
		ОписаниеТипаПлательщика = Новый ОписаниеТипов(Массив);
		Элементы.Плательщик.ОграничениеТипа = ОписаниеТипаПлательщика;        
	КонецЕсли;  
	
	Если Элементы.ДоговорКонтрагента.Видимость = Истина
		И ЗначениеЗаполнено(Объект.Плательщик) Тогда
		Элементы.ДоговорКонтрагента.Видимость = ПроверитьВидимостьДоговораВЗависимостиОтТипаКонтрагента(Объект.Плательщик);
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПроверитьВидимостьДоговораВЗависимостиОтТипаКонтрагента(Плательщик)
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Плательщик", Плательщик);
	Запрос.Текст = "ВЫБРАТЬ
	|    Контрагенты.ТипКонтрагента КАК ТипКонтрагента,
	|    Контрагенты.Ссылка КАК Контрагент
	|ИЗ
	|    Справочник.Контрагенты КАК Контрагенты
	|ГДЕ
	|    Контрагенты.Ссылка = &Плательщик";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Если ЗначениеЗаполнено(Выборка.ТипКонтрагента)
		И Выборка.ТипКонтрагента = Перечисления.ТипыКонтрагентов.Клиент Тогда
		возврат Ложь;
	Иначе
		возврат Истина;
	КонецЕсли;
КонецФункции 


&НаКлиенте
Процедура ТипДенежныхСредствПриИзменении(Элемент)
	МассивОчищаемыхРеквизитов = Новый Массив;
	Если Объект.ТипДенежныхСредств =  ПредопределенноеЗначение("Перечисление.ТипДенежныхСредств.Наличные")
		И ЗначениеЗаполнено(Объект.ЭквайринговыйТерминал) Тогда
		МассивОчищаемыхРеквизитов.Добавить("ЭквайринговыйТерминал");
		ТекстВопроса = "При изменении реквизита ""Тип денежных средств"" будут очищены следующие данные:
		| - Эквайринговый терминал
		| Продолжить?";
		
		
	ИначеЕсли Объект.ТипДенежныхСредств =  ПредопределенноеЗначение("Перечисление.ТипДенежныхСредств.Безналичные")
		И ЗначениеЗаполнено(Объект.Касса) Тогда 
		МассивОчищаемыхРеквизитов.Добавить("Касса");
		ТекстВопроса = "При изменении реквизита ""Тип денежных средств"" будут очищены следующие данные:
		| - Касса
		| Продолжить?"; 
	КонецЕсли;
	
	Если ЗначениеЗаполнено(МассивОчищаемыхРеквизитов) Тогда 
		Оповещение = Новый ОписаниеОповещения("ПослеОтветаНаВопросОбИзмененииТипаДенежныхСредств", ЭтаФорма, МассивОчищаемыхРеквизитов);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет,,,"Внимание!");
	Иначе
		ТипДенежныхСредств =  Объект.ТипДенежныхСредств;
		УстановитьВидимость(); 
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПослеОтветаНаВопросОбИзмененииТипаДенежныхСредств(Результат, 
	ДополнительныеПараметры) Экспорт
	Если Результат = КодВозвратаДиалога.Да Тогда
		Для Каждого Реквизит Из ДополнительныеПараметры Цикл
			Объект[Реквизит] = Неопределено;
		КонецЦикла;
		УстановитьВидимость();
		ТипДенежныхСредств =  Объект.ТипДенежныхСредств;
	Иначе
		Объект.ТипДенежныхСредств = ТипДенежныхСредств;
	КонецЕсли;
КонецПроцедуры 


&НаКлиенте
Процедура ВидОперацииПриИзменении(Элемент)
	МассивОчищаемыхРеквизитов = Новый Массив;
	Если (Объект.ВидОперации =  ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступленияДенег.ВозвратОтПодотчетника")
		ИЛИ Объект.ВидОперации =  ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступленияДенег.ПолучениеНаличныхВБанке"))
		И ЗначениеЗаполнено(Объект.ДоговорКонтрагента) Тогда
		МассивОчищаемыхРеквизитов.Добавить("ДоговорКонтрагента");
		
		ТекстВопроса = "При изменении реквизита ""Вид операции"" будут очищены следующие данные:
		| - Договор
		| Продолжить?";
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(МассивОчищаемыхРеквизитов) Тогда
		Оповещение = Новый ОписаниеОповещения("ПослеОтветаНаВопросОбИзмененииВидаОперации", ЭтаФорма, МассивОчищаемыхРеквизитов);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет,,,"Внимание!");
	Иначе
		ВидОперации =  Объект.ВидОперации;
		УстановитьВидимость();
	КонецЕсли;
КонецПроцедуры  

&НаКлиенте
Процедура ПослеОтветаНаВопросОбИзмененииВидаОперации(Результат, ДополнительныеПараметры) Экспорт
	Если Результат = КодВозвратаДиалога.Да Тогда
		Для Каждого Реквизит Из ДополнительныеПараметры Цикл
			Объект[Реквизит] = Неопределено;
		КонецЦикла;
		УстановитьВидимость();
		ВидОперации =  Объект.ВидОперации;
	Иначе
		Объект.ВидОперации = ВидОперации;
	КонецЕсли;
КонецПроцедуры  

&НаКлиенте
Процедура ПлательщикПриИзменении(Элемент)
	ЭтоПоставщик = ПроверитьВидимостьДоговораВЗависимостиОтТипаКонтрагента(Объект.Плательщик);
	МассивОчищаемыхРеквизитов = Новый Массив;
	
	Если  Не ЭтоПоставщик И ЗначениеЗаполнено(Объект.ДоговорКонтрагента) Тогда
		МассивОчищаемыхРеквизитов.Добавить("ДоговорКонтрагента");
		
		ТекстВопроса = "При изменении реквизита ""Плательщик"" будут очищены следующие данные:
		| - Договор
		| Продолжить?";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(МассивОчищаемыхРеквизитов) Тогда
		Оповещение = Новый ОписаниеОповещения("ПослеОтветаНаВопросОбИзмененииПлательщик", ЭтаФорма, МассивОчищаемыхРеквизитов);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет,,,"Внимание!");
	Иначе
		Плательщик =  Объект.Плательщик;
		УстановитьВидимость();
	КонецЕсли;
КонецПроцедуры
&НаКлиенте
Процедура ПослеОтветаНаВопросОбИзмененииПлательщик(Результат, ДополнительныеПараметры) Экспорт
	Если Результат = КодВозвратаДиалога.Да Тогда
		Для Каждого Реквизит Из ДополнительныеПараметры Цикл
			Объект[Реквизит] = Неопределено;
		КонецЦикла;
		УстановитьВидимость();
		Плательщик =  Объект.Плательщик;
	Иначе
		Объект.Плательщик = Плательщик;
	КонецЕсли;
КонецПроцедуры 

&НаКлиенте
Процедура СуммаДокументаПриИзменении(Элемент)
	Если ОсталосьОплатить > 0 И Объект.СуммаДокумента > ОсталосьОплатить Тогда
        Объект.СуммаДокумента = ОсталосьОплатить;
    КонецЕсли;КонецПроцедуры
