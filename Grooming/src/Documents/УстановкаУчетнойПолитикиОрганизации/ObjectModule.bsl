#Область ОбработчикиСобытий
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	Движения.УчетнаяПолитика.Записывать = Истина;
	Движение = Движения.УчетнаяПолитика.Добавить();
	Движение.Период = Дата;
	Движение.УчетнаяПолитика = УчетнаяПолитика;

	ДействующаяУчетнаяПолитика = Перечисления.ВидыУчетнойПолитики.ПустаяСсылка();
	РезультатДействующаяУчетнаяПолитика = РегистрыСведений.УчетнаяПолитика.СрезПоследних(Новый Граница(МоментВремени()));

	Если РезультатДействующаяУчетнаяПолитика.Количество() <> 0 Тогда
		ДействующаяУчетнаяПолитика = РезультатДействующаяУчетнаяПолитика.Получить(0).УчетнаяПолитика;
	Иначе
		ДвижениеПервое = Движения.УчетнаяПолитика.Добавить();
		ДвижениеПервое.Период = '00020101000000';
		ДвижениеПервое.УчетнаяПолитика = Перечисления.ВидыУчетнойПолитики.FEFO;

	КонецЕсли;

	Если УчетнаяПолитика = Перечисления.ВидыУчетнойПолитики.FEFO Тогда
		Константы.ИспользоватьСрокиГодности.Установить(Истина);
	КонецЕсли;

	Если УчетнаяПолитика = Перечисления.ВидыУчетнойПолитики.ПоСредней И ДействующаяУчетнаяПолитика
		<> Перечисления.ВидыУчетнойПолитики.ПоСредней Тогда

		Константы.ИспользоватьСрокиГодности.Установить(Ложь);
		Движения.ТоварыНаСкладах.Записывать = Истина;

		Блокировка = Новый БлокировкаДанных;
		Блокировка.Добавить("РегистрНакопления.ТоварыНаСкладах");
		Блокировка.Заблокировать();

		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура,
		|	ТоварыНаСкладахОстатки.СрокГодности КАК СрокГодности,
		|	ТоварыНаСкладахОстатки.Склад КАК Склад,
		|	ТоварыНаСкладахОстатки.КоличествоОстаток КАК Количество,
		|	ТоварыНаСкладахОстатки.СуммаОстаток КАК Сумма
		|ИЗ
		|	РегистрНакопления.ТоварыНаСкладах.Остатки(&МоментИтогов, ) КАК ТоварыНаСкладахОстатки
		|ИТОГИ
		|	СУММА(Количество),
		|	СУММА(Сумма)
		|ПО
		|	Номенклатура,
		|	Склад";

		Запрос.УстановитьПараметр("МоментИтогов", Новый Граница(МоментВремени()));

		РезультатЗапроса = Запрос.Выполнить();

		ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

		Пока ВыборкаНоменклатура.Следующий() Цикл

			ВыборкаНоменклатураСклад = ВыборкаНоменклатура.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаНоменклатураСклад.Следующий() Цикл

				Движение = Движения.ТоварыНаСкладах.ДобавитьПриход();
				Движение.Период = Дата;
				ЗаполнитьЗначенияСвойств(Движение, ВыборкаНоменклатураСклад, , "СрокГодности");

				Выборка = ВыборкаНоменклатураСклад.Выбрать();
				Пока Выборка.Следующий() Цикл
					Движение = Движения.ТоварыНаСкладах.ДобавитьРасход();
					Движение.Период = Дата;
					ЗаполнитьЗначенияСвойств(Движение, Выборка);

				КонецЦикла;

			КонецЦикла;

		КонецЦикла;

	КонецЕсли;
КонецПроцедуры
#КонецОбласти