#Область ОбработчикиСобытий
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	Если АвторДокумента.Пустая() Тогда
		АвторДокумента = ПараметрыСеанса.ТекущийПользователь;
	КонецЕсли;
КонецПроцедуры
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);

	Запрос.Текст =
	"ВЫБРАТЬ
	|	РасходДенежныхСредств.Дата КАК Дата,
	|	РасходДенежныхСредств.Касса КАК Касса,
	|	РасходДенежныхСредств.СуммаДокумента КАК СуммаДокумента,
	|	ЗНАЧЕНИЕ(Перечисление.ТипДенежныхСредств.Наличные) КАК ТипДенежныхСредств
	|ИЗ
	|	Документ.РасходДенежныхСредств КАК РасходДенежныхСредств
	|ГДЕ
	|	РасходДенежныхСредств.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	РасходДенежныхСредств.Дата,
	|	РасходДенежныхСредств.СуммаДокумента,
	|	РасходДенежныхСредств.Касса";

	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Движение = Движения.ДенежныеСредства.ДобавитьРасход();
		Движение.Период = Выборка.Дата;
		Движение.БанковскийСчетКасса = Выборка.Касса;
		Движение.Сумма = Выборка.СуммаДокумента;
		Движение.ТипДенежныхСредств = Выборка.ТипДенежныхСредств;
	КонецЦикла;

	Движения.ДенежныеСредства.Записывать = Истина;
	Движения.ДенежныеСредства.БлокироватьДляИзменения = Истина;

	Движения.Записать();
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДенежныеСредстваОстатки.БанковскийСчетКасса КАК Касса,
	|	ДенежныеСредстваОстатки.СуммаОстаток КАК Сумма
	|ИЗ
	|	РегистрНакопления.ДенежныеСредства.Остатки(
	|			&МоментВремени,
	|			БанковскийСчетКасса = &Касса
	|				И ТипДенежныхСредств = ЗНАЧЕНИЕ(Перечисление.ТипДенежныхСредств.Наличные)) КАК ДенежныеСредстваОстатки
	|ГДЕ
	|	ДенежныеСредстваОстатки.СуммаОстаток < 0";

	Запрос.УстановитьПараметр("Касса", Касса);
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(МоментВремени()));

	Результат = Запрос.Выполнить();

	Если Не Результат.Пустой() Тогда
		Отказ = Истина;
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтрШаблон("По кассе ""%1"" недостаточно денежных средств для расхода в размере %2",
			Выборка.Касса, Выборка.Сумма);
		Сообщение.Сообщить();

	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПоступлениеУслуг") Тогда
		ВидОперации = Перечисления.ВидыОперацийРасходаДенег.ОплатаПоставщику;
		ДоговорКонтрагента = ДанныеЗаполнения.ДоговорПоставщика;
		Получатель = ДанныеЗаполнения.Поставщик;
		ДокументОснование = ДанныеЗаполнения.Ссылка;
		СуммаДокумента = ДанныеЗаполнения.СуммаДокумента;
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПоступлениеТоваровИМатериалов") Тогда
		ВидОперации = Перечисления.ВидыОперацийРасходаДенег.ОплатаПоставщику;
		ДоговорКонтрагента = ДанныеЗаполнения.Договор;
		Получатель = ДанныеЗаполнения.Поставщик;
		ДокументОснование = ДанныеЗаполнения.Ссылка;
		СуммаДокумента = ДанныеЗаполнения.СуммаДокумента;
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.РеализацияТоваровИУслуг") Тогда
		ВидОперации = Перечисления.ВидыОперацийРасходаДенег.ВозвратДенегПокупателю;
		Получатель = ДанныеЗаполнения.Клиент;
		ДокументОснование = ДанныеЗаполнения.Ссылка;
		СуммаДокумента = ДанныеЗаполнения.СуммаДокумента;
	КонецЕсли;
КонецПроцедуры
#КонецОбласти
